const NULL 0
const true 1
const false 0

class cstr

  {
  args: pointer
  result: 
  
  prints a string
  }
  proc print 1 0 
    copy read NULL == if [disc ret]
    do
      { read the current char }
      copy
      read
      dump
  
      { increase the address }
      1 +
  
      { read the next address }
      copy
      readc
      
      0 !=
    end
    disc
    ret
  end

  {
  args:
  result:
  
  prints a new line
  }
  proc cr 0 0
    { print new line }
    10
    dump
    ret
  end

  {
  prints
  }
  proc println 1 0
    (cstr.print)
    (cstr.cr)
    ret
  end

  
  proc len 1 1
    copy readc 0 == if [disc 0 ret]
    copy
    do
      1 +
  
      copy readc
    end
    covr -
    swap disc
    ret
  end
  
  {
  args: pointer pointer
  result: int
  
  compares strings, returns 1 if they are not equal
  }
  proc eq 2 1
    covr readc
    covr readc
    != if [disc disc false ret]
    1 -
    swap
    1 -
    do
      1 +
      swap
      1 +
      covr readc
      covr readc
      covr 0 ==
      covr 0 == &&
      if [
        disc
        disc
        disc
        disc
        true
        ret
        ]
      covr 0 ==
      covr 0 == ||
      if [
        disc
        disc
        disc
        disc
        false
        ret
      ]
      ==
    end
    disc
    disc
    false
    ret
  end

  {
  args: pointer pointer
  result: 
  
  copy a string from a pointer to another
  }
  proc copy 2 2
    covr
    covr
    do
      covr
      covr
      readc
  
      putc
      disc
  
      swap 1 +
      swap 1 +
      copy readc 0 !=
    end
    0
    putc
    disc
    disc
    ret
  end
  
  {
  }
  proc last 1 1
    1 -
    do
      1 +
      copy readc
      0 !=
    end
    1 -
    readc
  
    ret
  end

  proc clear 1 1
    copy readc 0 == if [ret]
    copy
    do
      copy
      0
      putc
      disc
  
      1 +
      copy readc
    end
    disc
  
    ret
  end
  
  proc cat 2 1
    covr copy (cstr.len) +
    swap (cstr.copy) disc disc

    ret
  end

  gvar tempmin 1
  gvar tempmax 1
  
  {
  args: ptr[string]
  returns: int[val]
  
  returns 1 if the string is a number
  }
  proc isnum 1 1
    cstr.tempmin '0' putc disc
    cstr.tempmax '9' putc disc
  
    copy copy (cstr.len) + 1 -
    readc 
    copy 'b' == if [
      cstr.tempmax '1' putc disc
    ]
    copy 'h' == if [
      cstr.tempmax 'f' putc disc
    ]
    disc
    copy readc
    '-' == if [1 +]
    do
      copy readc
      copy cstr.tempmin readc <
      covr cstr.tempmax readc >
      || if [
        disc
        copy readc 'b' ==  
        covr readc 'h' == ||
        covr (cstr.len) 1 == &&
        swap disc
        ret
      ]
      disc
  
      1 +
      copy readc
      0 !=
    end
    disc
    1
    ret
  end
  
  gvar tempmul 1
  
  {
  args: ptr[string]
  returns: int[value]
  
  returns the value of a int
  }
  proc tonum 1 1
    copy (cstr.isnum) ! if [
      copy (cstr.println)
      "String is not a number" (cstr.println)
      disc
      -1 quit
    ]
      
    cstr.tempmul 10 putc disc
    
    copy copy (cstr.len) +
    copy readc 'b' == if [
      cstr.tempmul 2 putc disc
    ]
    disc
    
    0
    do
      cstr.tempmul readc *
      covr readc
      '0' - +
      
      swap 1 +
      swap
  
      covr readc
    end
    swap disc
  
    ret
  end

  gvar delim 1
  gvar tempstore 8

  {
  args: ptr[store] int[delim] ptr[cstr]
  result: ptr[store] ptr[cstr]
 
  splits a string on the delim
  }
  proc split 3 2
    swap cstr.delim swap putc disc
    cstr.tempstore swap put disc
  
    cstr.tempstore read (cstr.clear)

    {cstr}
    do
      covr readc
      cstr.delim readc ==
      if [
        disc 1 + cstr.tempstore read
        ret
      ]
      covr readc
      covr swap
      putc disc
      
      swap 1 +
      swap 1 +

      covr read 0 !=
    end
    
    ret
  end
end

class mem
  gvar tmp 8
  
  proc copy 3 0
    mem.tmp swap put disc
    do
      covr
      covr readc
      putc disc
  
      swap 1 +
      swap 1 +
  
      mem.tmp
      mem.tmp read
      1 - put disc
      mem.tmp read 0 >
    end
    disc
    disc
  
    ret
  end
  
  proc zero 2 0
    mem.tmp swap put disc
    do
      copy
      0
      putc disc
  
      1 +
  
      mem.tmp
      mem.tmp read
      1 - put disc
      mem.tmp read 0 >
    end
    disc
  
    ret
  end
end

class int
  prop data 8

  gvar tmpidx 8
 
  proc gettmp 0 1
    var 32 30 +
    int.tmpidx read -

    ret
  end

  proc inctmpidx 0 0
    int.tmpidx 
    int.tmpidx read 1 +
    putc disc

    ret
  end

  proc cstr 1 1
    do
      10 /
      '0' +
      (int.gettmp)
      swap
      putc disc
      (int.inctmpidx)
      
      copy 0 !=
    end
    disc
    (int.gettmp) 1 +
    
    int.tmpidx 0 putc disc
    ret
  end

  {
  args: ptr int
  result: ptr
  
  converts a int to a string
  }
  proc print 1 0
    (int.cstr) (cstr.print) (cstr.cr)
    
    int.tmpidx 0 putc disc
    ret
  end

  {
  args: ptr int
  result: ptr
  
  converts a int to a hex string
  }
  proc printhex 1 0
    do
      16 /
      copy 9 > if ['a' 10 - +]
      copy 10 < if ['0' +]
      (int.gettmp)
      swap
      putc disc
      (int.inctmpidx)

      copy 0 !=
    end
    disc
  
    (int.gettmp)
    'x'
    putc disc
    (int.inctmpidx)
  
    (int.gettmp)
    '0'
    putc disc
    (int.inctmpidx)
  
    (int.gettmp) 1 + (cstr.print)
    int.tmpidx 0 putc disc
    (cstr.cr)
    ret
  end
end

